buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

allprojects {

    group 'info.novatec.testit'
    version '0.3-SNAPSHOT'

    repositories {
        mavenCentral()
    }

}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = 1.8

    dependencies {

        compileOnly "org.projectlombok:lombok:1.16.16"

        testCompileOnly "org.projectlombok:lombok:1.16.16"

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M4'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:4.12.0-M4'

    }

    // produces a JavaDoc JAR
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    // produces a source code JAR
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives(javadocJar) // add JavaDoc JAR to archives set
        archives(sourcesJar) // add source code JAR to archives set
    }

    signing {
        sign(configurations.archives) // sign all archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    url 'https://github.com/nt-ca-aqe/testit-testutils'
                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    scm {
                        connection 'scm:git:git@github.com:nt-ca-aqe/testit-testutils.git'
                        developerConnection 'scm:git:git@github.com:nt-ca-aqe/testit-testutils.git'
                        url 'git@github.com:nt-ca-aqe/testit-testutils.git'
                    }
                    developers {
                        developer {
                            id 'slu'
                            name 'Stefan Ludwig'
                            email 'stefan.ludwig@novatec-gmbh.de'
                        }
                    }
                }
            }
        }
    }

}
